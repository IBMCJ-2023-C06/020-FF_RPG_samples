**FREE
//   H**********************************************************************
//   H*                                                                    *
//   H*   システム名      :ＲＰＧ教育                                *
//   H*   サブシステム名  :教育プログラム　　　　　　　　　　　      *
//   H*   プログラム名    :品目マスタ一覧表                          *
//   H*   プログラムＩＤ  : BPL010                                     *
//   H*   会　社　名　　  :株式会社中部システム                      *
//   H*                                                                    *
//   H*     作　成　者    :㈱中部システム Y.USHIDA                   *
//   H*     作　成　日    : 2023/05/30                                 *
//   H*     管　理　番　号: CSC-202305-001                             *
//   H*                                                                    *
//   H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
//   H*     変　更　日    : ____/__/__                                 *
//   H*     管　理　番　号:                                            *
//   H*                                                                    *
//   H*  プログラム特記事項                                              *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H*                                                                    *
//   H*-*******************************************************************
//   H*-*Ｈ仕様書                                                      **
//   H*-*******************************************************************
CTL-OPT
       DATEDIT(*YMD)
       DECEDIT('0.')
       COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
       DFTACTGRP(*NO) ACTGRP(*NEW)
;
//   F*-*******************************************************************
//   F*-*Ｆ仕様書                                                      **
//   F*-*******************************************************************
//   F*印刷装置
//DCL-F    BPL010P       PRINTER  OFLIND(*IN80                )                ;
DCL-F    BPL010P       PRINTER  OFLIND(W#FG_OF              )                ;
  DCL-S    W#FG_OF       IND                                                   ;
//   D*-*******************************************************************
//   D*-*Ｄ仕様書                                                      **
//   D*-*******************************************************************
//   D*-********************************************************************
//   D*-* データ構造（ＤＳ）                                            **
//   D*-********************************************************************
DCL-DS HINMSR        EXTNAME('HINMSP'  )      ;  //レコード構造
END-DS                                        ;
//   D*-********************************************************************
//   D*-* 変数定義                                                      **
//   D*-********************************************************************
//DCL-S  WTIMESTAMP    TIMESTAMP(*SYS )         ;  //タイムスタンプ
DCL-S  WTIMESTAMP    TIMESTAMP                ;  //タイムスタンプ
DCL-S  WDATE8        ZONED(8:0)               ;  //日付
DCL-S  WTIME6        ZONED(6:0)               ;  //時刻
DCL-S  W#CNTP        ZONED(9:0)               ;  //印刷件数
//   C*-********************************************************************
//   C*-* メインルーチン                                                **
//   C*-********************************************************************

//初期処理
EXSR      @INZ                         ;

IF @OPEN_CURSOR() ;
  DOW @FETCH_CURSOR() ;
    //印刷（明細
    EXSR      @PITEM1                      ;
  ENDDO              ;

  @CLOSE_CURSOR() ;

ENDIF ;

//印刷（見出し※０件の場合
IF W#CNTP     <= *ZERO                 ;
  EXSR      @PHEAD1                      ;
ENDIF                                  ;

//終了処理
EXSR      @END                         ;

//   C*-***************************************************************
//   C*-* @INZ        初期処理                                     **
//   C*-***************************************************************
BEGSR @INZ                                  ;

//システム日付
     WTIMESTAMP = %TIMESTAMP()                       ;
     WDATE8     = %DEC(%DATE(WTIMESTAMP) : *ISO )    ;
     WTIME6     = %DEC(%TIME(WTIMESTAMP) : *HMS )    ;

//変数の初期化
     //*IN(80) = *ON                                   ;    //オーバーフロー標識
     W#FG_OF = *ON                                   ;    //オーバーフロー標識
     W#CNTP  = *ZERO                                 ;    //印刷件数
ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @PHEAD1     印刷処理（見出し                             **
//   C*-***************************************************************
BEGSR @PHEAD1                               ;

     CLEAR PHEAD1                                    ;

     WRITE PHEAD1                                    ;

     //オーバーフロー標識オフ
     //*IN(80) = *OFF                                  ;    //オーバーフロー標識
     W#FG_OF = *OFF                                  ;    //オーバーフロー標識

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @PITEM1     印刷処理（明細                               **
//   C*-***************************************************************
BEGSR @PITEM1                               ;

     //見出し印刷
     //IF *IN80 = *ON          ;
     IF W#FG_OF  = *ON       ;
       EXSR @PHEAD1                               ;
     ENDIF                   ;

     CLEAR PITEM1                                    ;

     PI1HNBANG  =  HNBANG                            ; //品目番号
     PI1HNNAKJ  =  HNNAKJ                            ; //品目名（漢字
     PI1HNNAKN  =  HNNAKN                            ; //品目名（カナ
     PI1HNTEIK  =  HNTEIK                            ; //定価
     PI1HNGENK  =  HNGENK                            ; //原価
     PI1HNGSUR  =  HNGSUR                            ; //当月売上数量
     PI1HNGKIN  =  HNGKIN                            ; //当月売上高

     WRITE PITEM1                                    ;

     W#CNTP  += 1                                    ;    //印刷件数

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @END        終了処理                                     **
//   C*-***************************************************************
BEGSR @END                                  ;

     *INLR   = *ON                                   ;
     RETURN                                          ;

ENDSR                                       ;
//   P*-***************************************************************
//   P*-* < @OPEN_CURSOR  > :カーソルオープン処理                  **
//   P*-*------------------------------------------------------------**
//   P*-*          RETURN  : *ON=正常オープン,*OFF=エラー        **
//   P*-***************************************************************
DCL-PROC @OPEN_CURSOR                                                          ;
  DCL-PI *N                              IND                                   ;
  END-PI                                                                       ;

  DCL-S    W#RTN_VAR     IND                                                   ; //結果フラグ

     EXEC SQL
       DECLARE C1 CURSOR FOR
         SELECT *
           FROM HINMSP
           ORDER BY HNBANG
           FOR READ ONLY
     ;

     //カーソルオープン
     EXEC SQL        OPEN C1           ;

     IF SQLSTT = '00000' ;   // 00000:操作正常終了
          W#RTN_VAR  = *ON ;
     ELSE  ;
          W#RTN_VAR  = *OFF;
     ENDIF ;

     RETURN W#RTN_VAR  ;

END-PROC                                                                       ;
//   P*-***************************************************************
//   P*-* < @FETCH_CURSOR > :カーソル読取処理                      **
//   P*-*------------------------------------------------------------**
//   P*-*          RETURN  : *ON=正常読取,*OFF=ＥＯＦ又はエラー  **
//   P*-***************************************************************
DCL-PROC @FETCH_CURSOR                                                         ;
  DCL-PI *N                              IND                                   ;
  END-PI                                                                       ;

  DCL-S    W#RTN_VAR     IND                                                   ; //結果フラグ

     //カーソル読み込み
     EXEC SQL
       FETCH NEXT FROM C1 INTO :HINMSR
     ;

     SELECT ;
     WHEN SQLSTT = '00000' ;   // 00000:操作正常終了
         W#RTN_VAR  = *ON  ;
     WHEN SQLSTT = '02000' ;   // 02000:ＥＯＦ
         W#RTN_VAR  = *OFF ;
     OTHER ;                           //
         W#RTN_VAR  = *OFF ;
     ENDSL ;


     RETURN W#RTN_VAR  ;

END-PROC                                                                       ;
//   P*-***************************************************************
//   P*-* < @CLOSE_CURSOR > :カーソルクローズ処理                  **
//   P*-*------------------------------------------------------------**
//   P*-*          RETURN  : *ON=正常クローズ.*OFF=エラー        **
//   P*-***************************************************************
DCL-PROC @CLOSE_CURSOR                                                         ;
  DCL-PI *N                              IND                                   ;
  END-PI                                                                       ;

  DCL-S    W#RTN_VAR     IND                                                   ; //結果フラグ

     //カーソルクローズ
     EXEC SQL        CLOSE C1          ;

     IF SQLSTT = '00000' ;   // 00000:操作正常終了
          W#RTN_VAR  = *ON ;
     ELSE  ;
          W#RTN_VAR  = *OFF;
     ENDIF ;

     RETURN W#RTN_VAR  ;

END-PROC                                                                       ;
