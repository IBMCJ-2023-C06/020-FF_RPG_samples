**FREE
//   H**********************************************************************
//   H*                                                                    *
//   H*   システム名      :ＲＰＧ教育                                *
//   H*   サブシステム名  :教育プログラム　　　　　　　　　　　      *
//   H*   プログラム名    :品目マスタ一覧表                          *
//   H*   プログラムＩＤ  : BPL010                                     *
//   H*   会　社　名　　  :株式会社中部システム                      *
//   H*                                                                    *
//   H*     作　成　者    :㈱中部システム Y.USHIDA                   *
//   H*     作　成　日    : 2023/05/24                                 *
//   H*     管　理　番　号: CSC-202305-001                             *
//   H*                                                                    *
//   H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
//   H*     変　更　日    : ____/__/__                                 *
//   H*     管　理　番　号:                                            *
//   H*                                                                    *
//   H*  プログラム特記事項                                              *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H*                                                                    *
//   H*-*******************************************************************
//   H*-*Ｈ仕様書                                                      **
//   H*-*******************************************************************
//-------------------------------------------------------------------------
//制御仕様は先頭に宣言しなければならない
//-------------------------------------------------------------------------
CTL-OPT
       DATEDIT(*YMD)
       DECEDIT('0.')
       COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
;
//-------------------------------------------------------------------------
//変数宣言はメイン・ルーチンより前で行う必要がある
//-------------------------------------------------------------------------
//   D*-*******************************************************************
//   D*-*Ｄ仕様書                                                      **
//   D*-*******************************************************************
//   D*-********************************************************************
//   D*-* 変数定義                                                      **
//   D*-********************************************************************
//DCL-S  WTIMESTAMP    TIMESTAMP(*SYS )         ;  //タイムスタンプ
DCL-S  WTIMESTAMP    TIMESTAMP                ;  //タイムスタンプ
DCL-S  WDATE8        ZONED(8:0)               ;  //日付
DCL-S  WTIME6        ZONED(6:0)               ;  //時刻
DCL-S  W#CNTP        ZONED(9:0)               ;  //印刷件数

//-------------------------------------------------------------------------
//ファイル宣言はメイン・ルーチンより前で行う必要がある
//-------------------------------------------------------------------------
//   F*-*******************************************************************
//   F*-*Ｆ仕様書                                                      **
//   F*-*******************************************************************
//   F*印刷装置
//DCL-F    BPL010P       PRINTER  OFLIND(*IN80                )                ;
DCL-F    BPL010P       PRINTER  OFLIND(W#FG_OF              )                ;
  DCL-S    W#FG_OF       IND                                                   ;
//   F*品目マスタ
DCL-F    HINMSP        KEYED    USAGE(*INPUT                )                ;

//-------------------------------------------------------------------------
//ＬＩＫＥＲＥＣ等はファイル宣言より前で行う事は出来ない
//-------------------------------------------------------------------------
//   D*-********************************************************************
//   D*-* ＫＬＩＳＴ                                                    **
//   D*-********************************************************************
//品目マスタ
DCL-DS   HINMSKEY1     LIKEREC(HINMSR     : *KEY            ) INZ            ;

//   C*-********************************************************************
//   C*-* メインルーチン                                                **
//   C*-********************************************************************

//初期処理
EXSR      @INZ                         ;

//開始キー位置づけ

HINMSKEY1.HNBANG       = *LOVAL        ;      //品目番号
SETLL %KDS(HINMSKEY1   ) HINMSR        ;

DOW  1 = 1                             ;
  READ   HINMSR                          ;
  IF        %EOF                         ;
    LEAVE                                  ;
  ENDIF                                  ;
  //印刷（明細
  EXSR      @PITEM1                      ;
ENDDO                                  ;

//印刷（見出し※０件の場合
IF W#CNTP     <= *ZERO                 ;
  EXSR      @PHEAD1                      ;
ENDIF                                  ;

//終了処理
EXSR      @END                         ;

//   C*-***************************************************************
//   C*-* @INZ        初期処理                                     **
//   C*-***************************************************************
BEGSR @INZ                                  ;

//システム日付
     WTIMESTAMP = %TIMESTAMP()                       ;
     WDATE8     = %DEC(%DATE(WTIMESTAMP) : *ISO )    ;
     WTIME6     = %DEC(%TIME(WTIMESTAMP) : *HMS )    ;

//変数の初期化
     //*IN(80) = *ON                                   ;    //オーバーフロー標識
     W#FG_OF = *ON                                   ;    //オーバーフロー標識
     W#CNTP  = *ZERO                                 ;    //印刷件数
ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @PHEAD1     印刷処理（見出し                             **
//   C*-***************************************************************
BEGSR @PHEAD1                               ;

     CLEAR PHEAD1                                    ;

     WRITE PHEAD1                                    ;

     //オーバーフロー標識オフ
     //*IN(80) = *OFF                                  ;    //オーバーフロー標識
     W#FG_OF = *OFF                                  ;    //オーバーフロー標識

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @PITEM1     印刷処理（明細                               **
//   C*-***************************************************************
BEGSR @PITEM1                               ;

     //見出し印刷
     //IF *IN80 = *ON          ;
     IF W#FG_OF  = *ON       ;
       EXSR @PHEAD1                               ;
     ENDIF                   ;

     CLEAR PITEM1                                    ;

     PI1HNBANG  =  HNBANG                            ; //品目番号
     PI1HNNAKJ  =  HNNAKJ                            ; //品目名（漢字
     PI1HNNAKN  =  HNNAKN                            ; //品目名（カナ
     PI1HNTEIK  =  HNTEIK                            ; //定価
     PI1HNGENK  =  HNGENK                            ; //原価
     PI1HNGSUR  =  HNGSUR                            ; //当月売上数量
     PI1HNGKIN  =  HNGKIN                            ; //当月売上高

     WRITE PITEM1                                    ;

     W#CNTP  += 1                                    ;    //印刷件数

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @END        終了処理                                     **
//   C*-***************************************************************
BEGSR @END                                  ;

     *INLR   = *ON                                   ;
     RETURN                                          ;

ENDSR                                       ;
