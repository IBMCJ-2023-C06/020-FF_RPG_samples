**FREE
//   H**********************************************************************
//   H*                                                                    *
//   H*   システム名      :ＲＰＧ教育                                *
//   H*   サブシステム名  :教育プログラム　　　　　　　　　　　      *
//   H*   プログラム名    :得意先別受注一覧表                        *
//   H*   プログラムＩＤ  : BPL020                                     *
//   H*   会　社　名　　  :株式会社中部システム                      *
//   H*                                                                    *
//   H*     作　成　者    :㈱中部システム Y.USHIDA                   *
//   H*     作　成　日    : 2023/05/30                                 *
//   H*     管　理　番　号: CSC-202305-001                             *
//   H*                                                                    *
//   H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
//   H*     変　更　日    : ____/__/__                                 *
//   H*     管　理　番　号:                                            *
//   H*                                                                    *
//   H*  プログラム特記事項                                              *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H*                                                                    *
//   H*-*******************************************************************
//   H*-*Ｈ仕様書                                                      **
//   H*-*******************************************************************
CTL-OPT
       DATEDIT(*YMD)
       DECEDIT('0.')
       COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
;
//   F*-*******************************************************************
//   F*-*Ｆ仕様書                                                      **
//   F*-*******************************************************************
//   F*印刷装置
//DCL-F    BPL020P       PRINTER  OFLIND(*IN80                )                ;
DCL-F    BPL020P       PRINTER  OFLIND(W#FG_OF              )                ;
  DCL-S    W#FG_OF       IND                                                   ;
//   F*受注見出し
DCL-F    JUMIDL01      KEYED    USAGE(*INPUT                )                ;
//   F*得意先マスタ
DCL-F    TOKMSP        KEYED    USAGE(*INPUT                )                ;
//   D*-*******************************************************************
//   D*-*Ｄ仕様書                                                      **
//   D*-*******************************************************************
//   D*-********************************************************************
//   D*-* ＫＬＩＳＴ                                                    **
//   D*-********************************************************************
//受注見出
DCL-DS   JUMIDKEY1     LIKEREC(JUMIDR     : *KEY            ) INZ            ;
//得意先マスタ
DCL-DS   TOKMSKEY1     LIKEREC(TOKMSR     : *KEY            ) INZ            ;
//   D*-********************************************************************
//   D*-* 変数定義                                                      **
//   D*-********************************************************************
//DCL-S  WTIMESTAMP    TIMESTAMP(*SYS )         ;  //タイムスタンプ
DCL-S  WTIMESTAMP    TIMESTAMP                ;  //タイムスタンプ
DCL-S  WDATE8        ZONED(8:0)               ;  //日付
DCL-S  WTIME6        ZONED(6:0)               ;  //時刻
DCL-S  W#CNTP        ZONED(9:0)               ;  //印刷件数
//   C*-********************************************************************
//   C*-* メインルーチン                                                **
//   C*-********************************************************************

//初期処理
EXSR      @INZ                         ;

//開始キー位置づけ
JUMIDKEY1.JHTOKB       = *LOVAL        ;      //得意先番号
SETLL %KDS(JUMIDKEY1   ) JUMIDR        ;

DOW  1 = 1                             ;
  READ   JUMIDR                          ;
  IF        %EOF                         ;
    LEAVE                                  ;
  ENDIF                                  ;
  //印刷（明細
  EXSR      @PITEM1                      ;
ENDDO                                  ;

//印刷（見出し※０件の場合
IF W#CNTP     <= *ZERO                 ;
  EXSR      @PHEAD1                      ;
ENDIF                                  ;

//印刷（合計
EXSR      @PTOTAL1                     ;

//終了処理
EXSR      @END                         ;

//   C*-***************************************************************
//   C*-* @INZ        初期処理                                     **
//   C*-***************************************************************
BEGSR @INZ                                  ;

//システム日付
     WTIMESTAMP = %TIMESTAMP()                       ;
     WDATE8     = %DEC(%DATE(WTIMESTAMP) : *ISO )    ;
     WTIME6     = %DEC(%TIME(WTIMESTAMP) : *HMS )    ;

//変数の初期化
     //*IN(80) = *ON                                   ;    //オーバーフロー標識
     W#FG_OF = *ON                                   ;    //オーバーフロー標識
     W#CNTP  = *ZERO                                 ;    //印刷件数

     CLEAR PTOTAL1                                   ;

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @PHEAD1     印刷処理（見出し                             **
//   C*-***************************************************************
BEGSR @PHEAD1                               ;

     CLEAR PHEAD1                                    ;

     WRITE PHEAD1                                    ;

     //オーバーフロー標識オフ
     //*IN(80) = *OFF                                  ;    //オーバーフロー標識
     W#FG_OF = *OFF                                  ;    //オーバーフロー標識

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @PITEM1     印刷処理（明細                               **
//   C*-***************************************************************
BEGSR @PITEM1                               ;

     //見出し印刷
     //IF *IN80 = *ON          ;
     IF W#FG_OF  = *ON       ;
       EXSR @PHEAD1                               ;
     ENDIF                   ;

     CLEAR PITEM1                                    ;

     PI1JHTOKB  =  JHTOKB                            ; //得意先番号

     TOKMSKEY1.TKBANG     = JHTOKB          ;      //得意先番号
     CHAIN %KDS(TOKMSKEY1   ) TOKMSR        ;
     IF %FOUND              ;
       PI1TKNAKJ  =  TKNAKJ                          ; //得意先名（漢字
     ELSE                   ;
       PI1TKNAKJ  =  *ALL'*'                         ; //得意先名（漢字
     ENDIF                  ;

     PI1JHCHUB  =  JHCHUB                            ; //受注番号
     PI1JHGYOS  =  JHGYOS                            ; //明細行数
     PI1JHKING  =  JHKING                            ; //受注金額

     WRITE PITEM1                                    ;

     W#CNTP  += 1                                    ;    //印刷件数

     PT1KENSU   =  W#CNTP                            ; //レコード件数
     PT1GOKEI  +=  JHKING                            ; //受注金額

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @PTOTAL1    印刷処理（合計                               **
//   C*-***************************************************************
BEGSR @PTOTAL1                              ;

     IF W#CNTP <> *ZERO    ;
       EVAL(H) PT1HEIKIN  =  PT1GOKEI / W#CNTP       ; //平均金額
     ENDIF                 ;

     WRITE PTOTAL1                                   ;
     CLEAR PTOTAL1                                   ;

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @END        終了処理                                     **
//   C*-***************************************************************
BEGSR @END                                  ;

     *INLR   = *ON                                   ;
     RETURN                                          ;

ENDSR                                       ;
