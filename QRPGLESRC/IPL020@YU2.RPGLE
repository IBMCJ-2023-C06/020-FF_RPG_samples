**FREE
//   H**********************************************************************
//   H*                                                                    *
//   H*   システム名      :ＲＰＧ教育                                *
//   H*   サブシステム名  :教育プログラム　　　　　　　　　　　      *
//   H*   プログラム名    :品目マスタ一一覧照会                      *
//   H*   プログラムＩＤ  : IPL020                                     *
//   H*   会　社　名　　  :株式会社中部システム                      *
//   H*                                                                    *
//   H*     作　成　者    :㈱中部システム Y.USHIDA                   *
//   H*     作　成　日    : 2023/06/14                                 *
//   H*     管　理　番　号: CSC-202305-001                             *
//   H*                                                                    *
//   H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
//   H*     変　更　日    : ____/__/__                                 *
//   H*     管　理　番　号:                                            *
//   H*                                                                    *
//   H*  プログラム特記事項                                              *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H*                                                                    *
//   H*-*******************************************************************
//   H*-*Ｈ仕様書                                                      **
//   H*-*******************************************************************
CTL-OPT
       DATEDIT(*YMD)
       DECEDIT('0.')
       COPYRIGHT('...')
;
//   F*-*******************************************************************
//   F*-*Ｆ仕様書                                                      **
//   F*-*******************************************************************
//   F*印刷装置
DCL-F    IPL020D       WORKSTN  INDDS(DSP_PF    )
                                SFILE(SFL01:#RRN01 )                         ;
   DCL-DS DSP_PF                                                             ;
           F03         IND POS(03)     ;
           F12         IND POS(12)     ;
           SFLDSP      IND POS(32)     ;
           SFLCLR      IND POS(33)     ;
           SFLEND      IND POS(34)     ;
           ERR90       IND POS(90)     ;
   END-DS                                                                    ;
//   F*品目マスタ
DCL-F    HINMSL01      KEYED    USAGE(*INPUT                )                ;
//   D*-*******************************************************************
//   D*-*Ｄ仕様書                                                      **
//   D*-*******************************************************************
//   D*-********************************************************************
//   D*-* ＫＬＩＳＴ                                                    **
//   D*-********************************************************************
//品目マスタ
DCL-DS   HINMSKEY1     LIKEREC(HINMSR     : *KEY            ) INZ            ;
//   D*-********************************************************************
//   D*-* 変数定義                                                      **
//   D*-********************************************************************
//DCL-S  WTIMESTAMP    TIMESTAMP(*SYS )         ;  //タイムスタンプ
DCL-S  WTIMESTAMP    TIMESTAMP                ;  //タイムスタンプ
DCL-S  WDATE8        ZONED(8:0)               ;  //日付
DCL-S  WTIME6        ZONED(6:0)               ;  //時刻
DCL-S  W#CTL         CHAR(5)                  ;  //画面制御
DCL-S  #RRN01        ZONED(4:0)               ;  //ＳＦＬ相対レコード番号
//   C*-********************************************************************
//   C*-* メインルーチン                                                **
//   C*-********************************************************************

//初期処理
EXSR      @INZ                         ;

DOW  1 = 1                             ;
  SELECT                               ;
  WHEN   W#CTL = 'HED'                 ;
    EXSR  @HED                           ;
  WHEN   W#CTL = 'DTL'                 ;
    EXSR  @DTL                           ;
  OTHER                                ;
    LEAVE                                ;
  ENDSL                                  ;
ENDDO                                  ;

//終了処理
EXSR      @END                         ;

//   C*-***************************************************************
//   C*-* @INZ        初期処理                                     **
//   C*-***************************************************************
BEGSR @INZ                                  ;

//システム日付
     WTIMESTAMP = %TIMESTAMP()                       ;
     WDATE8     = %DEC(%DATE(WTIMESTAMP) : *ISO )    ;
     WTIME6     = %DEC(%TIME(WTIMESTAMP) : *HMS )    ;

//変数の初期化
     W#CTL   = 'HED'                                 ;    //画面制御

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @HED        画面処理（見出し                             **
//   C*-***************************************************************
BEGSR @HED                                  ;

     WRITE PANEL99                                   ;
     EXFMT PANEL01                                   ;

     IF  F03   = *ON                   ;
       W#CTL   = 'END'                                 ;    //画面制御
       LEAVESR                                         ;
     ENDIF                                  ;

  //入力チェック
     EXSR @CHKH                        ;
     IF  ERR90 = *ON                   ;   //エラーあり
       LEAVESR                                         ;
     ENDIF                                  ;

  //画面項目セット（明細
     EXSR @SETD                        ;

     //１件も存在しない
     IF  #RRN01 <= *ZERO               ;
       EVAL ERR90  = *ON        ;
       LEAVESR                                         ;
     ENDIF                                  ;

     W#CTL   = 'DTL'                                 ;    //画面制御

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @CHKH       チェック（見出                               **
//   C*-***************************************************************
BEGSR @CHKH                                 ;

      EVAL ERR90  = *OFF       ;

      //品目マスタ参照
      HINMSKEY1.HNNAKN       = X1NAKN        ;      //品目番号
      SETLL %KDS(HINMSKEY1   ) HINMSR        ;
      IF NOT %FOUND            ;
        EVAL ERR90  = *ON        ;
      ENDIF                    ;

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @SETD       画面項目セット                               **
//   C*-***************************************************************
BEGSR @SETD                                 ;

  //ＳＦＬクリアー
     EVAL SFLCLR = *ON                               ;
     EVAL SFLDSP = *OFF                              ;
     WRITE CTL01                                     ;
     EVAL SFLCLR = *OFF                              ;
     EVAL SFLDSP = *ON                               ;

  //レコード読み取り
     #RRN01      =  *ZERO                            ; //ＳＦＬ相対レコード番号
     EVAL SFLEND = *OFF                              ;
     DOW  1 = 1                             ;

       READ HINMSR                          ;
       IF %EOF                              ;
         EVAL SFLEND = *ON                               ;
         LEAVE                                ;
       ENDIF                                  ;

       CLEAR SFL01                                     ;

       #RRN01    +=  1                               ; //ＳＦＬ相対レコード番号
       DNO        =  #RRN01                          ; //行番号
       DHNBANG    =  HNBANG                          ; //品目番号
       DHNNAKJ    =  HNNAKJ                          ; //品目名（漢字）
       DHNTEIK    =  HNTEIK                          ; //定価
       DHNGENK    =  HNGENK                          ; //原価
       DHNGSUR    =  HNGSUR                          ; //当月売上数量
       DHNNSUR    =  HNNSUR                          ; //当年売上数量

       DHNNAKN    =  HNNAKN                          ; //品目カナ
       DHNGKIN    =  HNGKIN                          ; //当月売上金額
       DHNNKIN    =  HNNKIN                          ; //当年売上金額

       WRITE SFL01                                     ;

     ENDDO                                  ;


ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @DTL        画面処理（明細                               **
//   C*-***************************************************************
BEGSR @DTL                                  ;


     WRITE PANEL99                                   ;
     WRITE PANEL01                                   ;
     EXFMT CTL01                                     ;

     IF  F03   = *ON                   ;
       W#CTL   = 'END'                                 ;    //画面制御
       LEAVESR                                         ;
     ENDIF                                  ;

     W#CTL   = 'HED'                                 ;    //画面制御

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @END        終了処理                                     **
//   C*-***************************************************************
BEGSR @END                                  ;

     *INLR   = *ON                                   ;
     RETURN                                          ;

ENDSR                                       ;
