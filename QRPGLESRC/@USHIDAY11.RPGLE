**FREE
//   H**********************************************************************
//   H*                                                                    *
//   H*   システム名      :ＲＰＧ教育                                *
//   H*   サブシステム名  :その他                      　　　      *
//   H*   プログラム名    :ＦＦで許可されていない記述集１（ＦＦ      *
//   H*   プログラムＩＤ  : @USHIDAY11                                 *
//   H*   会　社　名　　  :株式会社中部システム                      *
//   H*                                                                    *
//   H*     作　成　者    :㈱中部システム Y.USHIDA                   *
//   H*     作　成　日    : 2023/06/14                                 *
//   H*     管　理　番　号: CSC-202305-001                             *
//   H*                                                                    *
//   H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
//   H*     変　更　日    : ____/__/__                                 *
//   H*     管　理　番　号:                                            *
//   H*                                                                    *
//   H*  プログラム特記事項                                              *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H*                                                                    *
//   H*-*******************************************************************
//   H*-*Ｈ仕様書                                                      **
//   H*-*******************************************************************
CTL-OPT
       DATEDIT(*YMD)
       DECEDIT('0.')
       COPYRIGHT('...')
;
//   D*-********************************************************************
//   D*-* 変数定義                                                      **
//   D*-********************************************************************
DCL-S  W#IX1           ZONED(9:0)                                                       ;
DCL-S  W#ZONE1         ZONED(9:0)                                                       ;
DCL-S  W#ZONE2         ZONED(9:0)                                                       ;
DCL-S  W#STR1          CHAR(64)                                                         ;
DCL-S  W#ARY1          CHAR(01)     DIM(10)                                             ;
DCL-S  W#ARY2          CHAR(01)     DIM(10)                                             ;
DCL-S  W#ARY3          ZONED(1:0)   DIM(10)                                             ;
DCL-S  W#DATE1         DATE         INZ(D'2023-01-01')                                  ;
//--------------------------------------------------------------------------------------
DCL-C  #ON             CONST(1    )                                                     ;
DCL-S  #DEFINE1        LIKE(W#ZONE1 )                                                   ;
//品目マスタ
DCL-F    HINMSP        KEYED    USAGE(*INPUT                )                ;
DCL-DS   #KLIST1       LIKEREC(HINMSR     : *KEY            ) INZ            ;
DCL-S  #TIME_1         TIMESTAMP                                             ;
//--------------------------------------------------------------------------------------
DCL-PR QCMDCHK         EXTPGM               ;
        *N CHAR(500)    OPTIONS(*VARSIZE) CONST        ;
        *N PACKED(15:5)                   CONST        ;
END-PR                                      ;
//--------------------------------------------------------------------------------------
//   C*-********************************************************************
//   C*-* 許可されていない記述                                          **
//   C*-********************************************************************
//   C*
//   C***************************
//   C*加算
//   C***************************
//   C                   ADD       1             W#ZONE1
//   C*
           W#ZONE1 += 1                                          ;

//   C***************************
//   C*期間の加算
//   C***************************
//   C                   ADDDUR    -1:*D         W#DATE1
//   C*
           W#DATE1 -= %DAYS(1)                                   ;

//   C***************************
//   C*比較＆分岐
//   C***************************
//   C     1             CABEQ     1             #CAB_1
//   C     #CAB_1        TAG
//   C*
           DOW #ON = #ON                                         ;
             IF 1 = 1                                              ;
               LEAVE                                                 ;
               //ITER                                                  ;
             ENDIF                                                 ;
           ENDDO                                                 ;

//   C***************************
//   C*ＣＡＬＬ（ＰＧＭ呼び出し
//   C***************************
//   C                   CALL      'QCMDCHK'
//   C                   PARM      'WRKSPLF'     P1@CMD          500
//   C                   PARM      007           P1@LEN           15 5
//   C*
//  「ＤＣＬ－ＰＲ」にてプロシージャを定義する
           CALLP QCMDCHK('WRKSPLF' :7)                                 ;
           QCMDCHK('WRKSPLF' :7)                                 ;

//   C***************************
//   C*比較＆サブルーチン
//   C***************************
//   C     1             CASEQ     2             #CAS_1
//   C                   ENDCS
//   C*
           IF 1 = 2                                              ;
             EXSR #CAS_1                                           ;
           ENDIF                                                 ;

//   C***************************
//   C*文字列連結
//   C***************************
//   C     'ABC'         CAT       'DEF'         W#STR1
//   C*
           W#STR1 = *ALL'-'                                      ;
           W#STR1 = 'ABC' + 'DEF' + W#STR1                       ;

//   C***************************
//   C*文字列検査
//   C***************************
//   C     '0123456789'  CHECK     '123A456':1   W#IX1
//   C     '0123456789'  CHECKR    '12Z3456'     W#IX1
//   C*
           W#IX1  = %CHECK('0123456789' :'123A456' :1 )          ;
           W#IX1  = %CHECKR('0123456789' :'12Z3456'    )          ;

//   C***************************
//   C*比較
//   C***************************
//   C     1             COMP      2                                  010203
//   C*
           *IN01   = *OFF                                        ;
           *IN02   = *OFF                                        ;
           *IN03   = *OFF                                        ;
           IF     1 > 2                                          ;
             *IN01   = *ON                                         ;
           ELSEIF 1 < 2                                          ;
             *IN02   = *ON                                         ;
           ELSEIF 1 = 2                                          ;
             *IN03   = *ON                                         ;
           ENDIF                                                 ;

//   C***************************
//   C*フィールド定義
//   C***************************
//   C     *LIKE         DEFINE    W#ZONE1       #DEFINE1
//   C*
//  「ＤＣＬ－Ｓ」にて変数を定義する
//         DCL-S  #DEFINE1        LIKE(W#ZONE1 )                          ;

//   C***************************
//   C*除算
//   C***************************
//   C     100           DIV(H)    3             W#ZONE1
//   C*
           EVAL(H) W#ZONE1 = 100 / 3                             ;
           W#ZONE1 = 100 / 3                                     ;
           W#ZONE1 = %DIV(100 : 3 )                              ;

//   C***************************
//   C*ＤＯ（命令の開始
//   C***************************
//   C                   DO
//   C                   ENDDO
//   C*
           FOR W#IX1 = 1 TO 1                                    ;
           ENDFOR                                                ;

//   C***************************
//   C*ＧＯＴＯ（スキップ
//   C***************************
//   C                   GOTO      #GOTO_1
//   C     #GOTO_1       TAG
//   C*
           DOW #ON = #ON                                         ;
             LEAVE                                                 ;
             //ITER                                                  ;
           ENDDO                                                 ;

//   C***************************
//   C*ＩＦｘｘ等
//   C***************************
//   C     1             IFEQ      1
//   C     1             ANDEQ     1
//   C     2             OREQ      2
//   C     3             ANDEQ     3
//   C                   ENDIF
//   C*
           IF     1 = 1
              AND 1 = 1
              OR  2 = 2
              AND 3 = 3                                          ;
           ENDIF                                                 ;

//   C***************************
//   C*キーリスト／フィールド
//   C***************************
//   C     #KLIST1       KLIST
//   C                   KFLD                    #KFLD1            1 0
//   C*
//  「ＤＣＬ－ＤＳ」にて変数を定義する
//         DCL-DS   #KLIST1       LIKEREC(HINMSR     : *KEY            ) INZ            ;

//   C***************************
//   C*配列探査
//   C***************************
//   C                   Z-ADD     1             W#IX1
//   C     '1'           LOOKUP    W#ARY1(W#IX1)                          01
//   C*
           W#IX1 = %LOOKUP('1' : W#ARY1 : 1)                     ;

//   C***************************
//   C*転送
//   C***************************
//   C                   MOVE(P)   '@'           W#STR1
//   C                   MOVEL(P)  '_'           W#STR1
//   C*
//  転送にはキャスト等を含む様々パターンがあり、一概に置き換えは出来ない
//  最も単純なケースは以下で代替え可能
           EVALR W#STR1 = '@'                                    ;
           W#STR1 = '_'                                          ;

//   C***************************
//   C*配列の転送
//   C***************************
//   C                   MOVEA     W#ARY1        W#ARY2
//   C*
           W#ARY2 = %SUBARR(W#ARY1 :1   )                        ;

//   C***************************
//   C*乗算
//   C***************************
//   C     7.7           MULT(H)   3.14          W#ZONE1
//   C*
           EVAL(H) W#ZONE1 = 7.7 * 3.14                           ;
           W#ZONE1 = 7.7 * 3.14                                   ;

//   C***************************
//   C*剰余
//   C***************************
//   C     100           DIV       3             W#ZONE1
//   C                   MVR                     W#ZONE2
//   C*
           W#ZONE2 = %REM(100 : 3)                               ;

//   C***************************
//   C*パラメータ
//   C***************************
//   C     #PLIST1       PLIST
//   C                   PARM                    #PARM1            1 0
//   C*
//  「ＤＣＬ－ＰＲ」にてプロシージャを定義する

//   C***************************
//   C*文字列走査
//   C***************************
//   C     '1'           SCAN      'ABC1DEF':1   W#IX1
//   C*
           W#IX1  = %SCAN('1' :'ABC1DEF':1  )          ;

//   C***************************
//   C*標識ＯＮ／ＯＦＦ
//   C***************************
//   C                   SETON                                        010203
//   C                   SETOFF                                       010203
//   C*
           *IN01 = *ON                                           ;
           *IN02 = *ON                                           ;
           *IN03 = *ON                                           ;
           *IN01 = *OFF                                          ;
           *IN02 = *OFF                                          ;
           *IN03 = *OFF                                          ;

//   C***************************
//   C*減算
//   C***************************
//   C                   SUB       1             W#ZONE1
//   C*
           W#ZONE1 -= 1                                          ;

//   C***************************
//   C*文字列切り取り
//   C***************************
//   C     2             SUBST     'ABC1DEF':3   W#STR1
//   C*
           W#STR1 = *ALL'-'                                      ;
           %SUBST(W#STR1 :1:2 ) = %SUBST( 'ABC1DEF' :3 :2)              ;

//   C***************************
//   C*タグ
//   C***************************
//   C     #TAG_1        TAG
//   C*
           DOW #ON = #ON                                         ;
             LEAVE                                                 ;
             //ITER                                                  ;
           ENDDO                                                 ;


//   C***************************
//   C*処理時刻
//   C***************************
//   C                   TIME                    #TIME_1          14 0
//   C*
           #TIME_1 = %TIMESTAMP()                               ;

//   C***************************
//   C*配列の合計
//   C***************************
//   C                   XFOOT     W#ARY3        W#ZONE1
//   C*
           W#ZONE1 =  %XFOOT(W#ARY3 )                            ;

//   C***************************
//   C*ゼロにして加算／減算
//   C***************************
//   C                   Z-ADD     *ZERO         W#ZONE1
//   C                   Z-SUB     *ZERO         W#ZONE1
//   C*
           W#ZONE1 = *ZERO                                       ;

//   C*-------------------------------------
//   C                   SETON                                        LR
//   C                   RETURN
//   C*
           *INLR   = *ON                                   ;
           RETURN                                          ;
//   C*-***************************************************************
//   C     #CAS_1        BEGSR
//   C                   ENDSR
BEGSR #CAS_1                                ;
ENDSR                                       ;
