**FREE
//   H**********************************************************************
//   H*                                                                    *
//   H*   システム名      :ＲＰＧ教育                                *
//   H*   サブシステム名  :教育プログラム　　　　　　　　　　　      *
//   H*   プログラム名    :品目マスタ一照会                          *
//   H*   プログラムＩＤ  : IPL010                                     *
//   H*   会　社　名　　  :株式会社中部システム                      *
//   H*                                                                    *
//   H*     作　成　者    :㈱中部システム Y.USHIDA                   *
//   H*     作　成　日    : 2023/06/12                                 *
//   H*     管　理　番　号: CSC-202305-001                             *
//   H*                                                                    *
//   H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
//   H*     変　更　日    : ____/__/__                                 *
//   H*     管　理　番　号:                                            *
//   H*                                                                    *
//   H*  プログラム特記事項                                              *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H* 　　　　　　　　　　　＿　　　　　　　　　　　　　　　　　　   *
//   H*                                                                    *
//   H*-*******************************************************************
//   H*-*Ｈ仕様書                                                      **
//   H*-*******************************************************************
CTL-OPT
       DATEDIT(*YMD)
       DECEDIT('0.')
       COPYRIGHT('...')
;
//   F*-*******************************************************************
//   F*-*Ｆ仕様書                                                      **
//   F*-*******************************************************************
//   F*印刷装置
DCL-F    IPL010D       WORKSTN  INDDS(DSP_PF    )                            ;
   DCL-DS DSP_PF                                                             ;
           F03         IND POS(03)     ;
           F12         IND POS(12)     ;
           PAGEUP      IND POS(33)     ;
           PAGEDOWN    IND POS(34)     ;
           ERR60       IND POS(60)     ;
           ERR61       IND POS(61)     ;
   END-DS                                                                    ;
//   F*品目マスタ
DCL-F    HINMSP        KEYED    USAGE(*INPUT                )                ;
//   D*-*******************************************************************
//   D*-*Ｄ仕様書                                                      **
//   D*-*******************************************************************
//   D*-********************************************************************
//   D*-* ＫＬＩＳＴ                                                    **
//   D*-********************************************************************
//品目マスタ
DCL-DS   HINMSKEY1     LIKEREC(HINMSR     : *KEY            ) INZ            ;
//   D*-********************************************************************
//   D*-* 変数定義                                                      **
//   D*-********************************************************************
//DCL-S  WTIMESTAMP    TIMESTAMP(*SYS )         ;  //タイムスタンプ
DCL-S  WTIMESTAMP    TIMESTAMP                ;  //タイムスタンプ
DCL-S  WDATE8        ZONED(8:0)               ;  //日付
DCL-S  WTIME6        ZONED(6:0)               ;  //時刻
DCL-S  W#CTL         CHAR(5)                  ;  //画面制御
//   C*-********************************************************************
//   C*-* メインルーチン                                                **
//   C*-********************************************************************

//初期処理
EXSR      @INZ                         ;

DOW  1 = 1                             ;
  SELECT                               ;
  WHEN   W#CTL = 'HED'                 ;
    EXSR  @HED                           ;
  WHEN   W#CTL = 'DTL'                 ;
    EXSR  @DTL                           ;
  OTHER                                ;
    LEAVE                                ;
  ENDSL                                  ;
ENDDO                                  ;

//終了処理
EXSR      @END                         ;

//   C*-***************************************************************
//   C*-* @INZ        初期処理                                     **
//   C*-***************************************************************
BEGSR @INZ                                  ;

//システム日付
     WTIMESTAMP = %TIMESTAMP()                       ;
     WDATE8     = %DEC(%DATE(WTIMESTAMP) : *ISO )    ;
     WTIME6     = %DEC(%TIME(WTIMESTAMP) : *HMS )    ;

//変数の初期化
     W#CTL   = 'HED'                                 ;    //画面制御

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @HED        画面処理（見出し                             **
//   C*-***************************************************************
BEGSR @HED                                  ;

     WRITE PANEL03                                   ;
     EXFMT PANEL01                                   ;
     EVAL ERR61  = *OFF       ;

     IF  F03   = *ON                   ;
       W#CTL   = 'END'                                 ;    //画面制御
       LEAVESR                                         ;
     ENDIF                                  ;

  //前を取得
     IF  PAGEUP     = *ON              ;
       EXSR @UP                                        ;
       LEAVESR                                         ;
     ENDIF                                  ;

  //次を取得
     IF  PAGEDOWN   = *ON              ;
       EXSR @DOWN                                      ;
       LEAVESR                                         ;
     ENDIF                                  ;

  //入力チェック
     EXSR @CHKH                        ;
     IF  ERR60 = *ON                   ;   //エラーあり
       LEAVESR                                         ;
     ENDIF                                  ;

     W#CTL   = 'DTL'                                 ;    //画面制御

  //画面項目セット（明細
     EXSR @SETD                        ;

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @UP         前を取得                                     **
//   C*-***************************************************************
BEGSR @UP                                   ;

      //開始キー位置づけ
      HINMSKEY1.HNBANG       = IX1BANG       ;      //品目番号
      SETLL %KDS(HINMSKEY1   ) HINMSR        ;
      READP HINMSR        ;
      IF NOT %EOF         ;
        IX1BANG  = HNBANG      ;
      ELSE                     ;
        EVAL ERR61  = *ON        ;
      ENDIF                    ;

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @DOWN       次を取得                                     **
//   C*-***************************************************************
BEGSR @DOWN                                 ;

      //開始キー位置づけ
      HINMSKEY1.HNBANG       = IX1BANG       ;      //品目番号
      SETGT %KDS(HINMSKEY1   ) HINMSR        ;
      READ  HINMSR        ;
      IF NOT %EOF         ;
        IX1BANG  = HNBANG      ;
      ELSE                     ;
        EVAL ERR61  = *ON        ;
      ENDIF                    ;

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @CHKH       チェック（見出                               **
//   C*-***************************************************************
BEGSR @CHKH                                 ;

      //品目マスタ参照
      EVAL ERR60  = *OFF       ;
      HINMSKEY1.HNBANG       = IX1BANG       ;      //品目番号
      CHAIN %KDS(HINMSKEY1   ) HINMSR        ;
      IF NOT %FOUND            ;
        EVAL ERR60  = *ON        ;
      ENDIF                    ;

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @SETD       画面項目セット                               **
//   C*-***************************************************************
BEGSR @SETD                                 ;

     IHNNAKJ    =  HNNAKJ                            ; //品目名（漢字）
     IHNNAKN    =  HNNAKN                            ; //品目名（カナ）
     IHNTEIK    =  HNTEIK                            ; //定価
     IHNGENK    =  HNGENK                            ; //原価
     IHNGSUR    =  HNGSUR                            ; //当月売上数量
     IHNNSUR    =  HNNSUR                            ; //当年売上数量

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @DTL        画面処理（明細                               **
//   C*-***************************************************************
BEGSR @DTL                                  ;


     WRITE PANEL03                                   ;
     WRITE PANEL01                                   ;
     EXFMT PANEL02                                   ;
     EVAL ERR61  = *OFF       ;

     IF  F03   = *ON                   ;
       W#CTL   = 'END'                                 ;    //画面制御
       LEAVESR                                         ;
     ENDIF                                  ;

  //前を取得
     IF  PAGEUP     = *ON              ;
       EXSR @UP                                        ;
       EXSR @SETD                        ;
       LEAVESR                                         ;
     ENDIF                                  ;

  //次を取得
     IF  PAGEDOWN   = *ON              ;
       EXSR @DOWN                                      ;
       EXSR @SETD                        ;
       LEAVESR                                         ;
     ENDIF                                  ;

     W#CTL   = 'HED'                                 ;    //画面制御

ENDSR                                       ;
//   C*-***************************************************************
//   C*-* @END        終了処理                                     **
//   C*-***************************************************************
BEGSR @END                                  ;

     *INLR   = *ON                                   ;
     RETURN                                          ;

ENDSR                                       ;
