     A*****************************************************************
     A**         品目マスター照会画面                              **
     A**          DISPLAY FILE(NAME---IPL010D)                       **
     A*****************************************************************
     A                                      PRINT
     A                                      CF03(03 '終了')
     A                                      INDARA
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL01)                     **
     A*****************************************************************
     A          R PANEL01                   OVERLAY
     A                                      PAGEUP(33)
     A                                      PAGEDOWN(34)
     A                                  1  3'IPL010'
     A                                  1 26'品目マスター照会'
     A                                      DSPATR(RI HI)
     A                                  1 63'日付'
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 63'時刻'
     A                                  2 70TIME
     A                                  5  4'品目番号・・・・・・'
     A            IX1BANG        5A  B  5 27
     A  60                              5 40'該当の品番が存在しません。'
     A                                      DSPATR(HI RI)
     A  61                              5 40'これ以上ありません。'
     A                                      COLOR(YLW)
     A                                      DSPATR(RI)
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL02)                     **
     A*****************************************************************
     A          R PANEL02                   OVERLAY PROTECT
     A                                      PAGEUP(33)
     A                                      PAGEDOWN(34)
     A                                 10  6'品目名（漢字）・・'
     A            IHNNAKJ       20O    10 27TEXT('品目名（漢字）')
     A                                 12  6'品目名（カナ）・・'
     A            IHNNAKN       20     12 27TEXT('品目名（カナ）')
     A                                 14  6'定価・・・・・・・'
     A            IHNTEIK        5  0  14 27TEXT('定価')
     A                                      EDTCDE(1)
     A                                 16  6'原価・・・・・・・'
     A            IHNGENK        5  0  16 27TEXT('原価')
     A                                      EDTCDE(1)
     A                                 18  6'当月売上数量・・・'
     A            IHNGSUR        5  0  18 27TEXT('当月売上数量')
     A                                      EDTCDE(1)
     A                                 20  6'当年売上数量・・・'
     A            IHNNSUR        5  0  20 27TEXT('当年売上数量')
     A                                      EDTCDE(1)
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL03)                     **
     A*****************************************************************
     A          R PANEL03
     A                                 23  3'F3=終了+
     A                                       ,PAGEUP:前+
     A                                       /PAGEDOWN:次'
