     A*****************************************************************
     A**          品目マスター照会画面                             **
     A**          DISPLAY FILE(NAME---IPL020D)                       **
     A*****************************************************************
     A                                      CA03(03)
     A                                      INDARA
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL01)                     **
     A*****************************************************************
     A          R PANEL01                   TEXT('見出しパネル')
     A                                      OVERLAY
     A                                  1  3'IPL020'
     A                                  1 30'品目マスター照会'
     A                                      DSPATR(HI)
     A                                  1 61'日付'
     A                                  1 68DATE(*YY) EDTCDE(Y)
     A                                  2 61'時刻'
     A                                  2 70TIME
     A                                  4  2'品目名（カナ）'
     A            X1NAKN         5W  B  4 21
     A  90                              4 40'該当のレコ－ドが存在しません。'
     A                                      DSPATR(RI HI)
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL99)                     **
     A*****************************************************************
     A          R PANEL99                   TEXT('コマンド指示パネル')
     A                                 23  3'F3 = 終了'
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---SFL01)                       **
     A*****************************************************************
     A          R SFL01                     TEXT('ｻﾌﾞﾌｧｲﾙﾚｺｰﾄﾞ')
     A                                      SFL
     A            DNO            2  0   9  2TEXT('行番号')
     A            DHNBANG        5      9  6TEXT('品目番号')
     A            DHNNAKJ       20      9 14TEXT('品目名（漢字）')
     A            DHNTEIK        5  0   9 37TEXT('定価')
     A                                      EDTCDE(1)
     A            DHNGENK        5  0   9 46TEXT('原価')
     A                                      EDTCDE(1)
     A            DHNGSUR        5  0   9 58TEXT('当月売上数量')
     A                                      EDTCDE(1)
     A            DHNNSUR        5  0   9 70TEXT('当年売上数量')
     A                                      EDTCDE(1)
     A*
     A            DHNNAKN       20A    10  7TEXT('品目カナ')
     A                                      COLOR(BLU)
     A            DHNGKIN        7  0  10 55TEXT('当月売上金額')
     A                                      EDTCDE(1)
     A            DHNNKIN        7  0  10 67TEXT('当年売上金額')
     A                                      EDTCDE(1)
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---CTL01)                       **
     A*****************************************************************
     A          R CTL01                     TEXT('ｻﾌﾞﾌｧｲﾙ制御ﾚｺｰﾄﾞ')
     A                                      SFLCTL(SFL01)
     A                                      SFLSIZ(24) SFLPAG(6)
     A                                      OVERLAY PROTECT
     A  32                                  SFLDSP SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*
     A                                  6 55'当月売上'
     A                                  6 67'当年売上'
     A*
     A                                  7  2'NO'
     A                                  7  5'品目'
     A                                  7 13'品目名'
     A                                  7 38'定価'
     A                                  7 47'原価'
     A                                  7 59'数量'
     A                                  7 71'数量'
     A                                  8  5'番号'
     A                                  8 59'金額'
     A                                  8 71'金額'
